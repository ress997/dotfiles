umask 022
bindkey -d
limit coredumpsize 0

if (( $+commands[rbenv] )); then
	eval "$(rbenv init -)"
fi

# ENV {{{

# AWS CLI
export AWS_CONFIG_FILE="$XDG_CONFIG_HOME/aws/config"
export AWS_CREDENTIAL_FILE="$DEV_DATA_HOME/secret/aws"

# Homebrew
if (( $+commands[brew] )); then
	export HOMEBREW_NO_ANALYTICS=1
fi

# enhancd
export ENHANCD_DIR="$XDG_DATA_HOME/enhancd"
export ENHANCD_DISABLE_HOME=1

# FZF
export FZF_DEFAULT_COMMAND='rg --files --hidden --glob "!.git"'
export FZF_DEFAULT_OPTS="--extended --ansi --multi"

# npm
export NPM_CONFIG_USERCONFIG="$XDG_CONFIG_HOME/npm/config"
export NPM_CONFIG_CACHE="$XDG_CACHE_HOME/npm"
export NOM_CONFIG_PREFIX="$XDG_DATA_HOME/npm"

# Gomi
export GOMI_DIR="$XDG_CACHE_HOME/gomi"

# Hub
export HUB_CONFIG="$XDG_CONFIG_HOME/hub/config.yaml"

# Tig
export TIGRC_USER="$XDG_CONFIG_HOME/tig/config"

# zplug
export ZPLUG_LOADFILE=$XDG_CONFIG_HOME/zplug/packages.zsh
export ZPLUG_CACHE_DIR="$XDG_CACHE_HOME/zplug"
export ZPLUG_CACHE_FILE="$ZPLUG_CACHE_DIR/cache"
export ZPLUG_REPOS="$ZPLUG_CACHE_DIR/repos"
export ZPLUG_FILTER=$FILTER
export ZPLUG_THREADS='32'

# }}}
# Auto Download {{{

# TMP
if (( $+commands[tmux] )) && [ ! -d $XDG_CACHE_HOME/tmux/tpm ]; then
	if (( $+commands[git] )); then
		git clone https://github.com/tmux-plugins/tpm $XDG_CACHE_HOME/tmux/tpm
	fi
	$XDG_CACHE_HOME/tmux/tpm/bin/install_plugins
fi

# nodebrew
[[ -d $NODEBREW_ROOT ]] || curl -L git.io/nodebrew | perl - setup

# zplug
[[ -d $ZPLUG_HOME ]] || curl -sL zplug.sh/installer | zsh

# }}}
# Tmux Auto new-session {{{
if (( $+commands[tmux] )); then
	if [ -z "$TMUX" ]; then
		if [ ! -z "$PS1" ] && [ -z "$SSH_CONECTION" ]; then
			if tmux has-session >/dev/null 2>&1 && tmux list-sessions | grep -qE '.*]$'; then
				tmux list-sessions
				echo -n "Tmux: attach? (y/N/num) "
				read
				if [[ "$REPLY" =~ ^[Yy]$ ]] || [[ "$REPLY" == '' ]]; then
					tmux attach-session
					if [ $? -eq 0 ]; then
						echo "$(tmux -V) attached session"
						return 0
					fi
				elif [[ "$REPLY" =~ ^[0-9]+$ ]]; then
					tmux attach -t "$REPLY"
					if [ $? -eq 0 ]; then
						echo "$(tmux -V) attached session"
						return 0
					fi
				fi
			fi
			if [[ "${(L)$( uname -s )}" == darwin ]] && (( $+commands[reattach-to-user-namespace] )); then
				tmux_config=$(cat $HOME/.tmux.conf <(echo 'set-option -g default-command "reattach-to-user-namespace -l $SHELL"'))
				tmux -f <(echo "$tmux_config") new-session && echo "$(tmux -V) created new session supported macOS"
			else
				tmux new-session && echo "tmux created new session"
			fi
		fi
	else
		echo "${fg_bold[red]} _____ __  __ _   ___  __ ${reset_color}"
		echo "${fg_bold[red]}|_   _|  \/  | | | \ \/ / ${reset_color}"
		echo "${fg_bold[red]}  | | | |\/| | | | |\  /  ${reset_color}"
		echo "${fg_bold[red]}  | | | |  | | |_| |/  \  ${reset_color}"
		echo "${fg_bold[red]}  |_| |_|  |_|\___//_/\_\ ${reset_color}"
	fi
fi
# }}}
# zplug {{{

if [[ -d $ZPLUG_HOME ]]; then
	source $ZPLUG_HOME/init.zsh
	zplug check || zplug install
	zplug load --verbose
fi

# }}}
# function {{{

# available
available () {
	local x candidates
	candidates="$1:"
	while [ -n "$candidates" ]; do
		x=${candidates%%:*}
		candidates=${candidates#*:}
		if type "$x" >/dev/null 2>&1; then
			echo "$x"
			return 0
		else
			continue
		fi
	done
	return 1
}

# ghq
if (( $+commands[ghq] )); then
	local DIRECTORY
	ghq-browse() {
		DIRECTORY=$(ghq list | $(available $FILTER) | cut -d "/" -f 2,3) && hub browse $DIRECTORY
	}
	ghq-update() {
		ghq list | sed 's|.[^/]*/||' | xargs -n 1 -P 10 ghq get -u
	}
fi

# Concurrency the mkdir and cd
mkcd() {
	if [[ -d $1 ]]; then
		echo "$1 already exists!"
		cd $1
	else
		mkdir -p $1 && cd $1
	fi
}


# Hub
(( $+commands[hub] )) && function git(){hub "$@"}

showoptions() {
	set -o | sed -e 's/^no\(.*\)on$/\1  off/' -e 's/^no\(.*\)off$/\1  on/'
}

# }}}
# autoload {{{

# TODO: å¾Œã§æ©Ÿèƒ½ã‚’èª¿ã¹ã‚‹
autoload -Uz modify-current-argument
autoload -Uz smart-insert-last-word
autoload -Uz terminfo
autoload -Uz zcalc

# }}}
# setopt {{{

# ãƒ’ã‚¹ãƒˆãƒª
setopt hist_ignore_all_dups		# åŒã˜ã‚³ãƒãƒ³ãƒ‰ã‚’ãƒ’ã‚¹ãƒˆãƒªã«æ®‹ã•ãªã„
setopt hist_ignore_space		# ã‚¹ãƒšãƒ¼ã‚¹ã‹ã‚‰å§‹ã¾ã‚‹ã‚³ãƒãƒ³ãƒ‰è¡Œã¯ãƒ’ã‚¹ãƒˆãƒªã«æ®‹ã•ãªã„
setopt hist_reduce_blanks		# ãƒ’ã‚¹ãƒˆãƒªã«ä¿å­˜ã™ã‚‹ã¨ãã«ä½™åˆ†ãªã‚¹ãƒšãƒ¼ã‚¹ã‚’å‰Šé™¤ã™ã‚‹
setopt share_history			# åŒæ™‚ã«èµ·å‹•ã—ãŸzshã®é–“ã§ãƒ’ã‚¹ãƒˆãƒªã‚’å…±æœ‰ã™ã‚‹

# ç„¡åŠ¹
setopt no_beep					# beep ã‚’ç„¡åŠ¹ã«ã™ã‚‹
setopt no_flow_control			# ãƒ•ãƒ­ãƒ¼ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ«ã‚’ç„¡åŠ¹ã«ã™ã‚‹

# æœªæ•´ç†
setopt always_last_prompt		# ã‚«ãƒ¼ã‚½ãƒ«ä½ç½®ã¯ä¿æŒã—ãŸã¾ã¾ãƒ•ã‚¡ã‚¤ãƒ«åä¸€è¦§ã‚’é †æ¬¡ãã®å ´ã§è¡¨ç¤º
setopt auto_menu				# è£œå®Œã‚­ãƒ¼é€£æ‰“ã§é †ã«è£œå®Œå€™è£œã‚’è‡ªå‹•ã§è£œå®Œ
setopt auto_param_keys			# ã‚«ãƒƒã‚³ã®å¯¾å¿œãªã©ã‚’è‡ªå‹•çš„ã«è£œå®Œ
setopt auto_param_slash			# ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªåã®è£œå®Œã§æœ«å°¾ã® / ã‚’è‡ªå‹•çš„ã«ä»˜åŠ ã—ã€æ¬¡ã®è£œå®Œã«å‚™ãˆã‚‹
setopt auto_list				# è£œå®Œå€™è£œã‚’ä¸€è¦§ã§è¡¨ç¤ºã™ã‚‹
setopt complete_in_word			# èªã®é€”ä¸­ã§ã‚‚ã‚«ãƒ¼ã‚½ãƒ«ä½ç½®ã§è£œå®Œ
setopt extended_glob			# é«˜æ©Ÿèƒ½ãªãƒ¯ã‚¤ãƒ«ãƒ‰ã‚«ãƒ¼ãƒ‰å±•é–‹ã‚’ä½¿ç”¨ã™ã‚‹
setopt globdots					# æ˜ç¢ºãªãƒ‰ãƒƒãƒˆã®æŒ‡å®šãªã—ã§.ã‹ã‚‰å§‹ã¾ã‚‹ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ãƒãƒƒãƒ
setopt ignore_eof				# Ctrl+Dã§zshã‚’çµ‚äº†ã—ãªã„
setopt interactive_comments		# ã‚³ãƒãƒ³ãƒ‰ãƒ©ã‚¤ãƒ³ã§ã‚‚ # ä»¥é™ã‚’ã‚³ãƒ¡ãƒ³ãƒˆã¨è¦‹ãªã™
setopt list_types				# è£œå®Œå€™è£œä¸€è¦§ã§ãƒ•ã‚¡ã‚¤ãƒ«ã®ç¨®åˆ¥ã‚’è­˜åˆ¥ãƒãƒ¼ã‚¯è¡¨ç¤º (è¨³æ³¨:ls -F ã®è¨˜å·)
setopt magic_equal_subst		# ã‚³ãƒãƒ³ãƒ‰ãƒ©ã‚¤ãƒ³ã®å¼•æ•°ã§ --prefix=/usr ãªã©ã® = ä»¥é™ã§ã‚‚è£œå®Œã§ãã‚‹
setopt mark_dirs				# ãƒ•ã‚¡ã‚¤ãƒ«åã®å±•é–‹ã§ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã«ãƒãƒƒãƒã—ãŸå ´åˆ æœ«å°¾ã« / ã‚’ä»˜åŠ 
setopt prompt_subst				# ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆãŒè¡¨ç¤ºã•ã‚Œã‚‹ãŸã³ã«ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆæ–‡å­—åˆ—ã‚’è©•ä¾¡ã€ç½®æ›ã™ã‚‹

# ãã®ä»–
setopt auto_cd
setopt auto_pushd				# cd ã—ãŸã‚‰è‡ªå‹•çš„ã«pushdã™ã‚‹
setopt pushd_ignore_dups		# é‡è¤‡ã—ãŸãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‚’è¿½åŠ ã—ãªã„
setopt pushd_to_home
setopt correct					# è£œå®Œæ©Ÿèƒ½
setopt correct_all
setopt brace_ccl				# Deploy {a-c} -> a b c
setopt print_eight_bit			# æ—¥æœ¬èªãƒ•ã‚¡ã‚¤ãƒ«åã‚’è¡¨ç¤ºå¯èƒ½ã«ã™ã‚‹
setopt equals					# Expand '=command' as path of command. e.g.) '=ls' -> '/bin/ls's

# }}}
# zstyle {{{

# è£œå®Œã§å°æ–‡å­—ã§ã‚‚å¤§æ–‡å­—ã«ãƒãƒƒãƒã•ã›ã‚‹
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# ../ ã®å¾Œã¯ä»Šã„ã‚‹ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‚’è£œå®Œã—ãªã„
zstyle ':completion:*' ignore-parents parent pwd ..

# ã“ã‚Œã‚‰ã®ãƒ•ã‚¡ã‚¤ãƒ«ã¯è£œå®Œå€™è£œã«ã—ãªã„ã€‚ã§ã‚‚ rm ã§ã¯å€™è£œã«ã™ã‚‹ã€‚
zstyle ':completion:*:*:(^rm):*:*files' ignored-patterns '*?.o|*?.d|*?.aux|*?~|*\#'

# ps ã‚³ãƒãƒ³ãƒ‰ã®ãƒ—ãƒ­ã‚»ã‚¹åè£œå®Œ
zstyle ':completion:*:processes' command "ps -u $USER -o pid,stat,%cpu,%mem,cputime,command"

# å€™è£œã«è‰²ä»˜ã‘?
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

# kill å€™è£œã‚’è‰²ä»˜ã‘
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([%0-9]#)*=0=01;31'

# è£œå®Œå€™è£œã‚’ã‚«ãƒ¼ã‚½ãƒ«ã§é¸ã¶ã€‚
zstyle ':completion:*:default' menu select=2

# è£œå®Œæ™‚ã«ã“ã‚Œã‚‰ã®ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã¯é™¤å¤–ã™ã‚‹ã€‚
zstyle ':completion:*:cd:*' ignored-patterns '*CVS|*.git|*lost+found'

# è£œå®Œãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã‚’èª­ã¿ã‚„ã™ãã™ã‚‹
zstyle ':completion:*' verbose yes
zstyle ':completion:*' format '%B%d%b'
zstyle ':completion:*:warnings' format 'No matches for: %d'
zstyle ':completion:*' group-name ''

# ã‚­ãƒ£ãƒƒã‚·ãƒ¥ã‚’ä½¿ã†?
zstyle ':completion:*' use-cache true

# ä¸æ˜ (è£œå®Œé–¢ä¿‚)
zstyle ':completion:*' completer _complete _match _approximate
zstyle ':completion:*:descriptions' format '%F{yellow}-- %d --%f'
zstyle ':completion:*:options' description 'yes'

# }}}
# aliases {{{

alias rm='rm -i'

# è¤‡æ•°ãƒ•ã‚¡ã‚¤ãƒ«ã®mv ä¾‹ã€€zmv *.txt *.txt.bk
autoload -Uz zmv
alias zmv='noglob zmv -W'

if [[ "${(L)$( uname -s )}" == darwin ]]; then
	alias ls='ls -G -F'
fi

alias la='ls -A'
alias ll='la -l'
alias lla='ls -lA'

# }}}
# ç•¥èªå±•é–‹(iab) {{{
typeset -A abbreviations
abbreviations=(
	# Global Alias
	"CP" "| pbcopy"
	"E" "| emojify"
	"J" "| jq ."
	# Docker
	"d" "docker"
	"da" "docker attach"
	"di" "docker images"
	"dl" "docker ps -l -q"
	"dp" "docker ps"
	"dpa" "docker ps -a"
	"dr" "docker run"
	"drm" "docker rm"
	"drmi" "docker rmi"
	"ds" "docker start"
	"djekyll" "docker run --rm --label=jekyll --volume=$(pwd):/srv/jekyll -it -p 80:4000 jekyll/jekyll:pages jekyll serve --watch --force_polling"
	# Gitç³»
	"ga"  "git add"
	"gb" "git branch"
	"gbd" "git branch -d"
	"gcm" "git commit"
	"gcmm" "git commit -m"
	"gco" "git checkout"
	"gcob" "git checkout -b"
	"gcobd" "git checkout -b develop master"
	"gd" "git diff"
	"gp" "git push"
	"gpo" "git push origin"
	"gpom" "git push origin master"
	"gs" "git status"
	"gst" "git status --short --branch"
	"gg" "ghq get"
	"gl" "ghq list"
	"ts" "tig status"
	# Homebrew
	"bc" "brew cleanup"
	"bd" "brew doctor"
	"bi" "brew install"
	"bl" "brew list"
	"bs" "brew search"
	"bu" "brew update"
	"bug" "brew upgrade"
	# ã‚¨ãƒ‡ã‚£ã‚¿
	"vim" "nvim"
	# miss
	"e" "exit"
	"sk" "ssh-keygen -t ed25519 -C 'email@example.com'"
	"t" "type"
)

magic-abbrev-expand() {
	local MATCH
	LBUFFER=${LBUFFER%%(#m)[-_a-zA-Z0-9]#}
	LBUFFER+=${abbreviations[$MATCH]:-$MATCH}
	zle self-insert

}

no-magic-abbrev-expand() {
	LBUFFER+=' '
}

zle -N magic-abbrev-expand
zle -N no-magic-abbrev-expand
bindkey " " magic-abbrev-expand
bindkey "^x " no-magic-abbrev-expand
# }}}
# keybinds {{{
bindkey -v

## Ctrl-R
__fzf-select-history() {
	if true; then
		BUFFER="$(
		history 1 \
		 | sort -k1,1nr \
		 | perl -ne 'BEGIN { my @lines = (); } s/^\s*\d+\s*//; $in=$_; if (!(grep {$in eq $_} @lines)) { push(@lines, $in); print $in; }' \
		 | $(available "fzf-tmux:fzf") --query "$LBUFFER" )"

		CURSOR=$#BUFFER
		zle reset-prompt
	fi
}
zle -N __fzf-select-history
bindkey '^r' __fzf-select-history
# }}}
# PROMPT {{{

pathshorten() {
    setopt localoptions noksharrays extendedglob
    local MATCH MBEGIN MEND
    local -a match mbegin mend
    "${2:-echo}" "${1//(#m)[^\/]##\//${MATCH/(#b)([^.])*/$match[1]}/}"
}

PROMPT_CHAR="â¯"

ON_COLOR="%{$fg[green]%}"
OFF_COLOR="%{$reset_color%}"
ERR_COLOR="%{$fg[red]%}"

__ultimate::prompt::user()
{
    echo "%(!.$ON_COLOR.$OFF_COLOR)$PROMPT_CHAR%{$reset_color%}"
}

__ultimate::prompt::job()
{
    echo "%(1j.$ON_COLOR.$OFF_COLOR)$PROMPT_CHAR%{$reset_color%}"
}

__ultimate::prompt::status()
{
    echo "%(0?.$ON_COLOR.$ERR_COLOR)$PROMPT_CHAR%{$reset_color%}"
}

__ultimate::prompt::path()
{
	local path_color="%{[38;5;244m%}%}"
	local rsc="%{$reset_color%}"
	local sep="$rsc/$path_color"
	local _path_="$(pathshorten "${PWD/$HOME/~}")"
	echo "$path_color$_path_$rsc"
}

PROMPT=""
PROMPT+='%{${fg[cyan]}%}%m%{${reset_color}%}'
PROMPT+=' :: '
PROMPT+='%{${fg[yellow]}%}%n%{${reset_color}%}'
PROMPT+=' :: '
PROMPT+='$(__ultimate::prompt::path)'
PROMPT+="
"
PROMPT+='$(__ultimate::prompt::user)'
PROMPT+='$(__ultimate::prompt::job)'
PROMPT+='$(__ultimate::prompt::status)'
PROMPT+=' '

PROMPT2=""
PROMPT2+='$(__ultimate::prompt::user)'
PROMPT2+='$(__ultimate::prompt::job)'
PROMPT2+='$(__ultimate::prompt::status)'
PROMPT2+=' '

SPROMPT="%{${fg[red]}%}Did you mean?: %R -> %r [nyae]? %{${reset_color}%}"

## vcs_info {{{

autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git

# ä»¥ä¸‹ã®3ã¤ã®ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã‚’ã‚¨ã‚¯ã‚¹ãƒãƒ¼ãƒˆã™ã‚‹
# - $vcs_info_msg_0_ : é€šå¸¸ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ç”¨ (ç·‘)
# - $vcs_info_msg_1_ : è­¦å‘Šãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ç”¨ (é»„è‰²)
# - $vcs_info_msg_2_ : ã‚¨ãƒ©ãƒ¼ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ç”¨ (èµ¤)
zstyle ':vcs_info:*' max-exports 3

# ãƒ•ã‚©ãƒ¼ãƒãƒƒãƒˆ
zstyle ':vcs_info:git:*' formats '%b' '%c%u %m'
zstyle ':vcs_info:git:*' actionformats '%b' '%c%u %m' '<!%a>'
zstyle ':vcs_info:git:*' check-for-changes true
zstyle ':vcs_info:git:*' stagedstr "+"    # %c ã§è¡¨ç¤ºã™ã‚‹æ–‡å­—åˆ—
zstyle ':vcs_info:git:*' unstagedstr "-"  # %u ã§è¡¨ç¤ºã™ã‚‹æ–‡å­—åˆ—


# formats '[%b]' '%c%u %m' , actionformats '[%b]' '%c%u %m' '<!%a>' ã®ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã‚’è¨­å®šã™ã‚‹ç›´å‰ã®ãƒ•ãƒƒã‚¯é–¢æ•°
zstyle ':vcs_info:git+set-message:*' hooks git-hook-begin git-untracked git-push-status

# ãƒ•ãƒƒã‚¯ã®æœ€åˆã®é–¢æ•°
# git ã®ä½œæ¥­ã‚³ãƒ”ãƒ¼ã®ã‚ã‚‹ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã®ã¿ãƒ•ãƒƒã‚¯é–¢æ•°ã‚’å‘¼ã³å‡ºã™ã‚ˆã†ã«ã™ã‚‹ (.git ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªå†…ã«ã„ã‚‹ã¨ãã¯å‘¼ã³å‡ºã•ãªã„) .git ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªå†…ã§ã¯ git status --porcelain ãªã©ãŒã‚¨ãƒ©ãƒ¼ã«ãªã‚‹ãŸã‚
+vi-git-hook-begin() {
	if [[ $(command git rev-parse --is-inside-work-tree 2> /dev/null) != 'true' ]]; then
		# 0ä»¥å¤–ã‚’è¿”ã™ã¨ãã‚Œä»¥é™ã®ãƒ•ãƒƒã‚¯é–¢æ•°ã¯å‘¼ã³å‡ºã•ã‚Œãªã„
		return 1
	fi

	return 0
}

# untracked ãƒ•ã‚¡ã‚¤ãƒ«è¡¨ç¤º
# untracked ãƒ•ã‚¡ã‚¤ãƒ«(ãƒãƒ¼ã‚¸ãƒ§ãƒ³ç®¡ç†ã•ã‚Œã¦ã„ãªã„ãƒ•ã‚¡ã‚¤ãƒ«)ãŒã‚ã‚‹å ´åˆã¯ unstaged (%u) ã« ? ã‚’è¡¨ç¤º
+vi-git-untracked() {
	# zstyle formats, actionformats ã®2ç•ªç›®ã®ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã®ã¿å¯¾è±¡ã«ã™ã‚‹
	if [[ "$1" != "1" ]]; then
		return 0
	fi

	if command git status --porcelain 2> /dev/null \
		| awk '{print $1}' \
		| command grep -F '??' > /dev/null 2>&1 ; then

		# unstaged (%u) ã«è¿½åŠ 
		hook_com[unstaged]+='?'
	fi
}

# push ã—ã¦ã„ãªã„ã‚³ãƒŸãƒƒãƒˆã®ä»¶æ•°è¡¨ç¤º
# ãƒªãƒ¢ãƒ¼ãƒˆãƒªãƒã‚¸ãƒˆãƒªã« push ã—ã¦ã„ãªã„ã‚³ãƒŸãƒƒãƒˆã®ä»¶æ•°ã‚’ pN ã¨ã„ã†å½¢å¼ã§ misc (%m) ã«è¡¨ç¤ºã™ã‚‹
+vi-git-push-status() {
	# zstyle formats, actionformats ã®2ç•ªç›®ã®ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã®ã¿å¯¾è±¡ã«ã™ã‚‹
	if [[ "$1" != "1" ]]; then
		return 0
	fi

	if [[ "${hook_com[branch]}" != "master" ]]; then
		return 0
	fi

	# push ã—ã¦ã„ãªã„ã‚³ãƒŸãƒƒãƒˆæ•°ã‚’å–å¾—ã™ã‚‹
	local ahead
	ahead=$(command git rev-list origin/master..master 2>/dev/null \
		| wc -l \
		| tr -d ' ')

	if [[ "$ahead" -gt 0 ]]; then
		# misc (%m) ã«è¿½åŠ 
		hook_com[misc]+="(p${ahead})"
	fi
}

_update_vcs_info_msg() {
	local -a messages
	local prompt

	LANG=en_US.UTF-8 vcs_info

	if [[ -z ${vcs_info_msg_0_} ]]; then
		# vcs_info ã§ä½•ã‚‚å–å¾—ã—ã¦ã„ãªã„å ´åˆã¯ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆã‚’è¡¨ç¤ºã—ãªã„
		prompt=""
	else
		# vcs_info ã§æƒ…å ±ã‚’å–å¾—ã—ãŸå ´åˆ $vcs_info_msg_0_ , $vcs_info_msg_1_ , $vcs_info_msg_2_ ã‚’ãã‚Œãã‚Œç·‘ã€é»„è‰²ã€èµ¤ã§è¡¨ç¤ºã™ã‚‹
		[[ -n "$vcs_info_msg_0_" ]] && messages+=( "%F{green}${vcs_info_msg_0_}%f" )
		[[ -n "$vcs_info_msg_1_" ]] && messages+=( "%F{yellow}${vcs_info_msg_1_}%f" )
		[[ -n "$vcs_info_msg_2_" ]] && messages+=( "%F{red}${vcs_info_msg_2_}%f" )

		# é–“ã«ã‚¹ãƒšãƒ¼ã‚¹ã‚’å…¥ã‚Œã¦é€£çµã™ã‚‹
		prompt="${(j: :)messages}"
	fi

	RPROMPT="$prompt"

}

add-zsh-hook precmd _update_vcs_info_msg

## }}}
# }}}
